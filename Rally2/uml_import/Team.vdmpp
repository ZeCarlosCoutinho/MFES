class Team
instance variables
  public team_members:set of (TeamMember);
  public cars:set of (Car);
  public manager:Manager;
  public sponsors:set of (Sponsor);
  public events:set of (Event);
  public name:seq of (char);

operations
  public  Team : seq of (char) * Manager ==> Team
  Team(nm, mngr) ==
    (
    name := nm;
    events := {};
    sponsors := {};
    cars := {};
    team_members := {mngr};
    manager := mngr;
    );

  public  addDriver : seq of (char) * seq of (char) ==> ()
  addDriver(nm, nati) ==
    (
    dcl driver : Driver := new Driver(nm, nati);
    team_members := team_members union {driver};
    )
    pre getMember(nm) = nil
    post getMember(nm) in set team_members;
  

  public  addCoDriver : seq of (char) * seq of (char) ==> ()
  addCoDriver(nm, nati) ==
    (
    dcl co_driver : CoDriver := new CoDriver(nm, nati);
    team_members := team_members union {co_driver};
    )
    pre getMember(nm) = nil
    post getMember(nm) in set team_members;


  public  addMechanic : seq of (char) * seq of (char) ==> ()
  addMechanic(nm, nati) ==
    (
    dcl mechanic : Mechanic := new Mechanic(nm, nati);
    team_members := team_members union {mechanic};
    )
    pre getMember(nm) = nil
    post getMember(nm) in set team_members;


  public  addCar : seq of (char) * seq of (char) * int * int * seq of (char) * int * int ==> ()
  addCar(make, model, year, bhp, traction, weight, number) ==
    (
    dcl car:Car := new Car(make, model, year, bhp, traction, weight, self, number);
    cars := cars union {car};
    )
    pre cars = {} or getCar(number) = nil
    post getCar(number) in set cars;

  public  assignDriver : [Car] * [Driver] ==> ()
  assignDriver(car, driver) ==
    (
    car.setDriver(driver);
    )
    pre car in set cars and driver in set team_members and car <> nil and driver <> nil
    post car.driver = driver;

  public  assignCoDriver : [Car] * [CoDriver] ==> ()
  assignCoDriver(car, co_driver) ==
    (
    car.setCoDriver(co_driver);
    )
    pre car in set cars and co_driver in set team_members and car <> nil and co_driver <> nil
    post car.co_driver = co_driver;

  public  assignMechanic : [Car] * [Mechanic] ==> ()
  assignMechanic(car, mechanic) ==
    (
    car.addMechanic(mechanic);
    )
    pre car in set cars and mechanic in set team_members and mechanic <> nil and car <> nil
    post mechanic in set car.mechanics;

  public  getEventResults : ()  ==> seq of (CarResult)
  getEventResults() ==
    (
	    dcl ret_seq:seq of (CarResult):= [];
	    for all event in set events do
	    (
	    	
	    	for all car in set cars do
	    	(
	    		if isofclass(SpecialStage, event)
	    		then(
	    			dcl ss: SpecialStage := event;
	    			dcl res:CarResult;
	    			dcl pos:int := 0;
	    			
	    			for n = 1 to len ss.getResults().stage_results do 
	    			(
	    				if ss.getResults().stage_results(n).car = car
	    				then pos := n;
	    			);
	    			
	    			res := new CarResult(car, event, pos);
	    			ret_seq := ret_seq ^ [res];
	    		);
	    		if isofclass(AtoB, event)
	    		then(
	    			dcl atob: AtoB := event;
	    			dcl res:CarResult;
	    			dcl pos:int := 0;
	    			
	    			for n = 1 to len atob.getFinalResults() do 
	    			(
	    				if atob.getFinalResults()(n).car = car
	    				then pos := n;
	    			);
	    			
	    			res := new CarResult(car, event, pos);
	    			ret_seq := ret_seq ^ [res];
	    		);
	    	);
	    );
	    return ret_seq;
    );
    --pre true
    --post len RESULT <= card events * card cars;

  public pure  getCar : int ==> [Car]
  getCar(n) ==
    (
		dcl ret_car:[Car] := nil;
		
		for all car in set cars do
			if car.number = n
			then return car;
		return ret_car;
		)
		pre cars <> {}
		post RESULT in set cars or RESULT = nil;

  public pure  getMember : seq of (char) ==> [TeamMember]
  getMember(nm) ==
    (
		dcl ret_member:[TeamMember] := nil;
		
		for all member in set team_members do
			if member.name = nm
			then return member;
		return ret_member;
		)
		pre team_members <> {}
		post RESULT in set team_members or RESULT = nil;
		
	public  addSponsor : Sponsor ==> ()
  addSponsor(spsr) ==
    (
    sponsors := sponsors union {spsr};
    )
    pre spsr not in set sponsors
    post spsr in set sponsors;
  
  public  addEvent : Event ==> ()
  addEvent(event) ==
    (
    events := events union {event};
    )
    pre event not in set events
    post event in set events;
	
end Team
